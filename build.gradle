plugins {
  id 'java-library'
  id 'jacoco'
  id 'io.spring.dependency-management' version '1.1.7'
  id 'com.github.ben-manes.versions' version '0.53.0'
  id 'org.sonarqube' version '7.2.2.6593'
  id 'maven-publish'
  /*
    Applies analysis tools including checkstyle and OWASP Dependency checker.
    See https://github.com/hmcts/gradle-java-plugin
 */
  id 'uk.gov.hmcts.java' version '0.12.67'
}

group = 'uk.gov.hmcts'
version = '0.2.4'

java {
  toolchain {
    languageVersion = JavaLanguageVersion.of(21)
  }
  withSourcesJar()   // generates sources JAR
}

def configureSourceSet(String name) {
  sourceSets.create(name) { sourceSet ->
    sourceSet.java {
      compileClasspath += sourceSets.main.output + sourceSets.test.output
      runtimeClasspath += sourceSets.main.output + sourceSets.test.output
      srcDir "src/${name}/java"
    }
    sourceSet.resources.srcDir "src/${name}/resources"
  }
}

['smokeTest', 'integrationTest', 'functionalTest'].each { configureSourceSet(it) }

configurations {
  functionalTestImplementation.extendsFrom testImplementation
  functionalTestRuntimeOnly.extendsFrom runtimeOnly

  integrationTestImplementation.extendsFrom testImplementation
  integrationTestRuntimeOnly.extendsFrom runtimeOnly

  smokeTestImplementation.extendsFrom testImplementation
  smokeTestRuntimeOnly.extendsFrom runtimeOnly
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << '-parameters'
}

tasks.withType(JavaCompile).configureEach {
  options.compilerArgs << "-Xlint:unchecked" << "-Werror"
}

tasks.withType(Test).configureEach {
  useJUnitPlatform()
  failFast = true

  testLogging {
    exceptionFormat = 'full'
  }
}

tasks.register('functional', Test) {
  description = "Runs functional tests"
  group = "Verification"
  testClassesDirs = sourceSets.functionalTest.output.classesDirs
  classpath = sourceSets.functionalTest.runtimeClasspath
}

tasks.register('integration', Test) {
  description = "Runs integration tests"
  group = "Verification"
  testClassesDirs = sourceSets.integrationTest.output.classesDirs
  classpath = sourceSets.integrationTest.runtimeClasspath
  failFast = true
}

tasks.register('smoke', Test) {
  description = "Runs Smoke Tests"
  group = "Verification"
  testClassesDirs = sourceSets.smokeTest.output.classesDirs
  classpath = sourceSets.smokeTest.runtimeClasspath
}

jacocoTestReport {
  executionData(test, integration)
  reports {
    xml.getRequired().set(true)
    csv.getRequired().set(false)
    html.getRequired().set(true)
  }
}

tasks.sonarqube.dependsOn(jacocoTestReport)
tasks.check.dependsOn(integration)

sonarqube {
  properties {
    property "sonar.projectName", "HMCTS :: opal-common-lib"
    property "sonar.projectKey", "uk.gov.hmcts:opal-common-lib"
  }
}

// before committing a change, make sure task still works
dependencyUpdates {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { qualifier -> version.toUpperCase().contains(qualifier) }
    def regex = /^[0-9,.v-]+$/
    return !stableKeyword && !(version ==~ regex)
  }
  rejectVersionIf { selection -> // <---- notice how the closure argument is named
    return isNonStable(selection.candidate.version) && !isNonStable(selection.currentVersion)
  }
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
  suppressionFile = 'config/owasp/suppressions.xml'
}

repositories {
  mavenLocal()
  mavenCentral()
  maven {
    url = uri('https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1')
  }
}

ext {
  log4JVersion = "2.25.3"
  logbackVersion = "1.5.32"
  lombokVersion = "1.18.42"
  mapstructVersion = "1.6.3"
  springVersion = "3.5.11"
}

ext['snakeyaml.version'] = '2.0'

dependencyManagement {
  imports {
    mavenBom 'org.springframework.boot:spring-boot-dependencies:3.5.11'
    mavenBom 'org.springframework.cloud:spring-cloud-dependencies:2025.1.1'
  }
}

dependencies {
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: springVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: springVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: springVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json', version: springVersion
  implementation group: 'org.springframework.boot', name: 'spring-boot-starter-oauth2-resource-server', version: springVersion
  implementation 'org.springframework.cloud:spring-cloud-starter-openfeign:4.3.1', {
    exclude group: 'commons-fileupload', module: 'commons-fileupload'
  }
  implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:3.0.1',{
    exclude group: 'org.springframework.boot', module: 'spring-boot-jackson'
    exclude group: 'org.springframework.boot', module: 'spring-boot-webmvc'
    exclude group: 'org.springframework.boot', module: 'spring-boot-web-server'
    exclude group: 'org.springframework.boot', module: 'spring-boot-validation'
    exclude group: 'org.hibernate.validator', module: 'hibernate-validator'   // Excluded for CVE-2025-15104
  }
  implementation 'org.hibernate.validator:hibernate-validator:9.1.0.Final'    // CVE-2025-15104 not yet fixed, so suppressed in owasp file

  implementation group: 'com.github.hmcts.java-logging', name: 'logging', version: '7.0.0'

  implementation 'com.microsoft.azure:applicationinsights-spring-boot-starter:2.6.4'

  implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: log4JVersion
  implementation group: 'org.apache.logging.log4j', name: 'log4j-to-slf4j', version: log4JVersion
  implementation group: 'ch.qos.logback', name: 'logback-classic', version: logbackVersion
  implementation group: 'ch.qos.logback', name: 'logback-core', version: logbackVersion
  implementation group: 'org.mapstruct', name: 'mapstruct', version: mapstructVersion

  compileOnly "org.projectlombok:lombok:${lombokVersion}"
  annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  annotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  integrationTestCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  integrationTestAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  integrationTestAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  functionalTestCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  functionalTestAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  functionalTestAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  smokeTestCompileOnly "org.projectlombok:lombok:${lombokVersion}"
  smokeTestAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
  smokeTestAnnotationProcessor "org.mapstruct:mapstruct-processor:${mapstructVersion}"

  testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: springVersion
  testImplementation group: 'io.rest-assured', name: 'rest-assured', version: '5.5.7'
  testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

  testImplementation 'javax.servlet:javax.servlet-api:4.0.1'
}

jar {
  archiveClassifier.set('')
}

publishing {
  repositories {
    maven {
      name = "AzureArtifacts"
      url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
      credentials {
        username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
        password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
      }
    }
  }
  publications {
    Main(MavenPublication) {
      from components.java
      groupId = project.group
      artifactId = 'opal-common-lib'
      version = project.version
    }
  }
}

// Gradle 7.x issue, workaround from: https://github.com/gradle/gradle/issues/17236#issuecomment-894768083
rootProject.tasks.named("processSmokeTestResources") {
  duplicatesStrategy = 'include'
}

wrapper {
    distributionType = Wrapper.DistributionType.ALL
}
